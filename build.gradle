plugins {
    id 'java'
    id 'jacoco'
    id 'io.freefair.lombok' version '9.1.0'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.tds'
version = '0.0.1-SNAPSHOT'
description = 'Parking management API'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'
}

tasks.named('test') {
    systemProperty('junit.jupiter.displayname.generator.default', 'org.junit.jupiter.api.DisplayNameGenerator$ReplaceUnderscores')
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = '0.8.13'
}

jacocoTestReport {
    reports {
        html.required = true
    }
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 1.0
            }
        }
    }
}

check.dependsOn(jacocoTestCoverageVerification)
